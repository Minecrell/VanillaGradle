plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'eclipse'
    id 'idea'

    id 'maven-publish'
    id 'nebula.provided-base' version '3.1.0'
    id 'com.gradle.plugin-publish' version '0.9.7'

    id 'net.minecrell.licenser' version '0.3'
}

group = 'net.minecrell'
version = '2.2-6-SNAPSHOT'
description = 'ForgeGradle 2 extension for Vanilla mods'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

archivesBaseName = project.name.toLowerCase()

repositories {
    jcenter()
    maven {
        name = 'forge'
        url = 'http://files.minecraftforge.net/maven'
    }
}

dependencies {
    compile localGroovy()
    provided 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
}

license {
    include '**/*.java'
    include '**/*.groovy'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

pluginBundle {
    website = 'https://github.com/Minecrell/VanillaGradle'
    vcsUrl = website
    description = project.description
    tags = ['minecraft', 'mcp', 'sponge']

    plugins {
        serverPlugin {
            id = 'net.minecrell.vanillagradle.server'
            displayName = 'VanillaGradle server plugin'
            description = 'Provides a MCP workspace for the Vanilla server with all run tasks disabled'
        }
        clientPlugin {
            id = 'net.minecrell.vanillagradle.client'
            displayName = 'VanillaGradle client plugin'
            description = 'Provides a MCP workspace for the Vanilla client with all run tasks disabled'
        }
    }

    mavenCoordinates {
        artifactId = project.archivesBaseName
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId project.archivesBaseName

            from components.java

            artifact sourceJar

            repositories {
                maven {
                    url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}
