plugins {
    id 'java-gradle-plugin'
    id 'groovy'

    id 'eclipse'
    id 'idea'

    id 'maven-publish'

    id 'com.github.hierynomus.license' version '0.11.0'
}

group = 'net.minecrell'
version = '2.0.3-SNAPSHOT'

sourceCompatibility = 1.6
targetCompatibility = 1.6

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

eclipse {
    classpath {
        plusConfigurations += [configurations.provided]
    }
}

idea {
    module {
        scopes.PROVIDED.plus += [configurations.provided]
    }
}

repositories {
    jcenter()
    maven {
        name = 'forge'
        url = 'http://files.minecraftforge.net/maven'
    }
}

dependencies {
    compile localGroovy()
    provided 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
}

license {
    header file('LICENSE')
    include '**/*.java'
    include '**/*.groovy'

    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourceJar

            repositories {
                maven {
                    url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$buildDir/repo"
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
